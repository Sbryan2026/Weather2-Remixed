buildscript
{
	repositories
	{
		mavenCentral()
		maven {url = 'https://maven.minecraftforge.net/'}
		maven
		{
			name = 'sponge'
			url = 'https://repo.spongepowered.org/repository/maven-public'
		}
	}
	dependencies
	{
		classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.73'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
		
apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

project.mkdir "libs"
java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

version = mod_version
group = mod_group
archivesBaseName = mod_name

mixin {
    add sourceSets.main, 'META-INF/weather2_mixin_refmap.json'
    config 'META-INF/weather2_mixin.json'
    debug.strict.targets = false
}

repositories
{
	maven
	{
		name = "Blame jared for this one"
		url = "https://maven.blamejared.com/"
	}
	maven
	{
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
	maven
	{
		name = 'SpongePowered'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
   	}
}

dependencies
{
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
 	runtimeOnly fg.deobf("mezz.jei:jei_${minecraft_version}:${jei_version}")
 	implementation files("libs/${coroutil_jar}")
	implementation "org.spongepowered:mixin:0.8.5"
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
	testAnnotationProcessor "org.spongepowered:mixin:0.8.5:processor"
	//compileOnly files("libs/OptiFine_1.12.2_HD_U_G5.jar")
}

minecraft
{
	mappings channel: minecraft_mappings_channel, version: minecraft_mappings_version
	accessTransformer = file("src/main/resources/META-INF/weather2_at.cfg")
	
	runs
	{
		client
		{
			workingDirectory project.file(client_directory)
			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
		}

		server
		{
			workingDirectory project.file(server_directory)
			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
            arg '-torg.spongepowered.asm.launch.MixinTweaker'
		}
	}
}

jar
{
	from {configurations.compileClasspath.filter {"mixin-0.8.5.jar" == it.name}.collect{it.isDirectory() ? it : zipTree(it)}}
	{
		exclude 'LICENSE.txt', 'module-info.class', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
	}
	manifest
	{
		attributes (
			'FMLAT': 'META-INF/weather2_at.cfg',
			'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'MixinConfigs': 'META-INF/weather2_mixin.json',
            'FMLCorePlugin': 'net.mrbt0907.weather2.Weather2MixinThing',
			'FMLCorePluginContainsFMLMod': 'true',
            "ForceLoadAsMod": 'true'
		)
	}
	
	finalizedBy 'reobfJar'
}

processResources
{
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	
	inputs.property 'version', mod_version
	inputs.property 'mcversion', minecraft_version
	from(sourceSets.main.resources.srcDirs)
	{
		include 'mcmod.info'
		expand 'version':mod_version, 'mcversion':minecraft_version
	}
	from(sourceSets.main.resources.srcDirs)
	{
		exclude 'mcmod.info'
	}
}

//When Forge 1.12 loads mods from a directory that's been put on the classpath, it expects to find resources in the same directory.
//Default Gradle behavior puts resources in ./build/resources/main instead of ./build/classes/main/java. Let's change that.
sourceSets.all {it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next()}